module cpp-classes

imports

  c-lexical
  c-syntax
  namespaces
  
context-free sorts

  ClassTypeSpecifier
  ClassType ExtensionDeclaration
  
context-free syntax

  ClassTypeSpecifier.ClassType = <
    <ClassType> <TypeID> {
      <ClassBody> 
    }
  >
  
  ClassTypeSpecifier.ClassTypeExtended = <
    <ClassType> <TypeID>: <{ExtensionDeclaration ","}+> {
      <ClassBody>
    }
  > 
  
  ClassTypeSpecifier.ClassTypeDecl = <
    <ClassType> <NamespaceTypeID>
  > 
  
  ClassType.Class  = <class>
  ClassType.Struct = <struct> 
  ClassType.Union  = <union>
  
  ExtensionDeclaration.ExtensionDeclaration = <<VisibilityModifier> <NamespaceTypeID>>

context-free sorts

  ClassBody ClassBodyDeclaration
  ClassBodyDeclarationDefault VisibilityModifier
  ClassBodyDeclarator
  
context-free syntax
  
  ClassBody.ClassBody = <
  	<ClassBodyDeclarationDefault>
    <ClassBodyDeclaration*>
  >
  
  ClassBodyDeclarationDefault.ClassBodyDeclarationDefault = <
      <ClassBodyDeclarator*>
  >
  ClassBodyDeclaration.ClassBodyDeclaration = <
    <VisibilityModifier> :
      <ClassBodyDeclarator*>
  >
  
  VisibilityModifier.Private = <private>
  VisibilityModifier.Protected = <protected> 
  VisibilityModifier.Public = <public>
  
  ClassBodyDeclarator = Declaration
  ClassBodyDeclarator = FunctionDefinition  
  
context-free sorts

  ClassDeclarationSpecifier
  
context-free syntax

  ClassDeclarationSpecifier.Virtual = <virtual>
  ClassDeclarationSpecifier.Explicit = <explicit>
  
  
  