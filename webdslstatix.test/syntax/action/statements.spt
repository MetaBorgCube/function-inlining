module syntax/action/statements

language webdsl-statix

fixture [[
  application action_statements

  function f() {
    [[...]]
  }
]]

// expression as statement
test expression add as statment [[
  1+2;
]] parse succeeds parse to Stat(Add(Const(Int("1")), Const(Int("2"))))

test function call as statement [[
  x.f();
]] parse succeeds parse to Stat(Call(Var("x"), "f", []))

// block
test block as statement [[
  { }
]] parse succeeds parse to Block([])

test block as statement nested [[
  { { } }
]] parse succeeds parse to Block([Block([])])

test block multiple statements [[
  {
    var x : Int;
    var y : String;
  }
]] parse succeeds parse to Block([VarDecl("x", SimpleSort("Int")), VarDecl("y", SimpleSort("String"))])


// return statement
test return statement [[
  return 1;
]] parse succeeds parse to Return(Const(Int("1")))

test return statement nested [[
  return 1+2;
]] parse succeeds parse to Return(Add(Const(Int("1")), Const(Int("2"))))

test return empty [[
  return;
]] parse succeeds parse to ReturnEmpty()

test dead code [[
  return 1;
  return 2;
]] parse succeeds parse to !ATerm [Return(Const(Int("1"))), Return(Const(Int("2")))]


// go to statement
test goto [[
  goto root();
]] parse succeeds parse to GoTo(PageCall("root", []))

test goto [[
  goto ( root() );
]] parse succeeds parse to GoToAlt(PageCall("root", []))

// variable declaration
test var decl [[
  var x : Int;
]] parse succeeds parse to VarDecl("x", SimpleSort("Int"))

test var decl init [[
  var x : Int := 1;
]] parse succeeds parse to VarDeclInit("x", SimpleSort("Int"), Const(Int("1")))

test var decl init inferred [[
  var x := 1;
]] parse succeeds parse to VarDeclInitInferred("x", Const(Int("1")))

// if statements

test if else [[
  if (false) { }
  else { }
]] parse succeeds parse to If(False(), Block([]), Block([]))

test if no else [[
  if (false) { }
]] parse succeeds parse to IfNoElse(False(), Block([]))

test if nested condition [[
  if (1+2 >= 3) { }
]] parse succeeds parse to IfNoElse(LargerThanOrEqual(Add(Const(Int("1")),Const(Int("2"))),Const(Int("3"))), Block([]))

test if else if else [[
  if (false) { }
  else if (false) { }
  else { }
]] parse succeeds parse to IfWithElseIf(False(), Block([]), [ElseIf(False(), Block([]))], OptElseSome(Block([])))

test if else if [[
  if (false) { } else if (false) { }
]] parse succeeds parse to IfWithElseIf(False(), Block([]), [ElseIf(False(), Block([]))], OptElseNone())

test multiple else ifs [[
  if (false) { }
  else if (false) {  }
  else if (false) {  }
]] parse succeeds parse to IfWithElseIf(False(), Block([]), [ElseIf(False(), Block([])), ElseIf(False(), Block([]))], OptElseNone())

// case statements

test case statement [[
  case (x) {
    1 { }
    2 { }
  }
]] parse succeeds parse to CaseStmt([Var("x")], [CaseAlt(CaseExps([Const(Int("1"))]), Block([])), CaseAlt(CaseExps([Const(Int("2"))]), Block([]))])

test case statement stmts[[
  case (x) {
    1 {
        var y : Int;
        var z : String;
      }
    2 {
        var y : Int;
        var z : String;
      }
  }
]] parse succeeds parse to CaseStmt([Var("x")], [CaseAlt(CaseExps([Const(Int("1"))]), Block([VarDecl("y",SimpleSort("Int")), VarDecl("z",SimpleSort("String"))])), CaseAlt(CaseExps([Const(Int("2"))]), Block([VarDecl("y",SimpleSort("Int")),VarDecl("z",SimpleSort("String"))]))])

test case statement no cases [[
  case (x) { }
]] parse succeeds parse to CaseStmt([Var("x")], [])

test case statement expression as case [[
  case (x+x) {
    1+1 { }
  }
]] parse succeeds parse to CaseStmt([Add(Var("x"), Var("x"))], [CaseAlt(CaseExps([Add(Const(Int("1")), Const(Int("1")))]), Block([]))])

test case statement no expressions [[
  case () { }
]] parse fails

test case statement multiple expressions [[
  case (x, y) { }
]] parse succeeds parse to CaseStmt([Var("x"), Var("y")], [])

test case statement multiple expressions and cases [[
  case (x, y) {
    1, 1 { }
    2, 2 { }
  }
]] parse succeeds parse to CaseStmt([Var("x"), Var("y")], [CaseAlt(CaseExps([Const(Int("1")), Const(Int("1"))]), Block([])), CaseAlt(CaseExps([Const(Int("2")), Const(Int("2"))]), Block([]))])

test case statement only default [[
 case (x) {
    default { }
  }
]] parse succeeds parse to CaseStmt([Var("x")], [CaseAltDefault(Block([]))])

test case statement default last [[
 case (x) {
    1 { }
    default { }
  }
]] parse succeeds parse to CaseStmt([Var("x")], [CaseAlt(CaseExps([Const(Int("1"))]), Block([])), CaseAltDefault(Block([]))])

test case statement default first [[
 case (x) {
    default { }
    1 { }
  }
]] parse succeeds parse to CaseStmt([Var("x")], [CaseAltDefault(Block([])), CaseAlt(CaseExps([Const(Int("1"))]), Block([]))])

// typecase statements

test typecase [[
  typecase (x) { }
]] parse succeeds parse to TypeCaseStmt(Var("x"), OptTypeAliasNone(), [])

test typecase alias [[
  typecase (x as y) {  }
]] parse succeeds parse to TypeCaseStmt(Var("x"), TypeAlias("y"), [])

test typecase single case [[
  typecase (x) {
    Int { }
  }
]] parse succeeds parse to TypeCaseStmt(Var("x"), OptTypeAliasNone(), [TypeCaseAlt(SimpleSort("Int"), Block([]))])

test typecase multiple cases [[
  typecase (x) {
    Int { }
    String { }
  }
]] parse succeeds parse to TypeCaseStmt(Var("x"), OptTypeAliasNone(), [TypeCaseAlt(SimpleSort("Int"), Block([])), TypeCaseAlt(SimpleSort("String"), Block([]))])

test typecase default case [[
  typecase (x) {
    default { }
  }
]] parse succeeds parse to TypeCaseStmt(Var("x"), OptTypeAliasNone(), [TypeCaseAltDefault(Block([]))])

test typecase multiple stmts [[
  typecase (x) {
    Int {
      var y : Int;
      var z : String;
    }
  }
]] parse succeeds parse to TypeCaseStmt(Var("x"), OptTypeAliasNone(), [TypeCaseAlt(SimpleSort("Int"), Block([VarDecl("y",SimpleSort("Int")),VarDecl("z",SimpleSort("String"))]))])

test typecase default last [[
  typecase (x) {
    Int { }
    default { }
  }
]] parse succeeds parse to TypeCaseStmt(Var("x"), OptTypeAliasNone(), [TypeCaseAlt(SimpleSort("Int"), Block([])), TypeCaseAltDefault(Block([]))])

test typecase default first [[
  typecase (x) {
    default { }
    Int { }
  }
]] parse succeeds parse to TypeCaseStmt(Var("x"), OptTypeAliasNone(), [TypeCaseAltDefault(Block([])), TypeCaseAlt(SimpleSort("Int"), Block([]))])


// for and while loops
test for loop [[
  for (x : Int in xs) { }
]] parse succeeds parse to ForStmt("x", SimpleSort("Int"), Var("xs"), OptFilterNone(), Block([]))

test for loop multiple stmts [[
  for (x : Int in xs) {
    var y : Int;
    var z : String;
  }
]] parse succeeds parse to ForStmt("x", SimpleSort("Int"), Var("xs"), OptFilterNone(), Block([VarDecl("y",SimpleSort("Int")),VarDecl("z",SimpleSort("String"))]))

test filter for loop [[
  for (x : Ent in xs where x.y == 1) { }
]] parse succeeds parse to ForStmt("x", SimpleSort("Ent"), Var("xs"), OptFilterSome(FilterNoOrderByNoLimit(Eq(FieldAccess(FAVar("x"), "y"), Const(Int("1"))))), Block([]))

test for loop inferred [[
  for (x in xs) { }
]] parse succeeds parse to ForStmtInferred("x", Var("xs"), OptFilterNone(), Block([]))

test for loop inferred multiple stmts [[
  for (x in xs) {
    var y : Int;
    var z : String;
  }
]] parse succeeds parse to ForStmtInferred("x", Var("xs"), OptFilterNone(), Block([VarDecl("y",SimpleSort("Int")),VarDecl("z",SimpleSort("String"))]))

test filter inferred for loop [[
  for (x in xs where x.y == 1) { }
]] parse succeeds parse to ForStmtInferred("x", Var("xs"), OptFilterSome(FilterNoOrderByNoLimit(Eq(FieldAccess(FAVar("x"), "y"), Const(Int("1"))))), Block([]))

test for all loop [[
  for (u : User) { }
]] parse succeeds parse to ForAllStmt("u", SimpleSort("User"), OptFilterNone(), Block([]))

test for all loop multiple stmts [[
  for (u : User) {
    var y : Int;
    var z : String;
  }
]] parse succeeds parse to ForAllStmt("u", SimpleSort("User"), OptFilterNone(), Block([VarDecl("y",SimpleSort("Int")),VarDecl("z",SimpleSort("String"))]))

test filter for all loop [[
  for (u : User where u.x == 1) { }
]] parse succeeds parse to ForAllStmt("u", SimpleSort("User"), OptFilterSome(FilterNoOrderByNoLimit(Eq(FieldAccess(FAVar("u"), "x"), Const(Int("1"))))), Block([]))

test for count statement [[
  for (x : Int from 13 to 37) { }
]] parse succeeds parse to ForCountStmt("x", Const(Int("13")), Const(Int("37")), Block([]))

test for count statement multiple stmts [[
  for (x : Int from 13 to 37) {
    var y : Int;
    var z : String;
  }
]] parse succeeds parse to ForCountStmt("x", Const(Int("13")), Const(Int("37")), Block([VarDecl("y",SimpleSort("Int")),VarDecl("z",SimpleSort("String"))]))

test for count statement no filter [[
  for (x : Int from 13 to 37 where x > 1) { }
]] parse fails

test while statement [[
  while (1>2) { }
]] parse succeeds parse to WhileStmt(LargerThan(Const(Int("1")),Const(Int("2"))), Block([]))

test while statement multiple stmts [[
  while (1>2) {
    var y : Int;
    var z : String;
  }
]] parse succeeds parse to WhileStmt(LargerThan(Const(Int("1")),Const(Int("2"))), Block([VarDecl("y",SimpleSort("Int")),VarDecl("z",SimpleSort("String"))]))

// reassignment
test assignment [[ x := 2; ]] parse succeeds parse to Assignment(Var("x"), Const(Int("2")))
test field assignment [[ x.y := 2; ]] parse succeeds parse to Assignment(FieldAccess(FAVar("x"), "y"), Const(Int("2")))
