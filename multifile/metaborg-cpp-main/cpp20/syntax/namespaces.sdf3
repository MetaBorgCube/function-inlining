module namespaces

imports
  c-lexical
  c-syntax
  cpp20

sorts NamespaceDeclaration NamespaceID NamespaceTypeID NamespaceTypeID-OPT NamespaceVarOrTypeID NamespacePrefix UsingDeclaration InlineModifier

context-free syntax

InlineModifier.InlineModifier = <inline>

NamespaceID.NamespaceID = <<NamespacePrefix?><ID>>
NamespaceTypeID.NamespaceTypeID = <<NamespacePrefix?><TypeID>>

NamespaceTypeID-OPT = NamespaceTypeID
NamespaceTypeID-OPT.NoNamespaceTypeID = <>

NamespaceVarOrTypeID = NamespaceID
NamespaceVarOrTypeID = NamespaceTypeID

NamespacePrefix.Prefix = <<{ID "::"}+>::>

NamespaceDeclaration.Namespace = <<InlineModifier?> namespace <NamespaceID> {
	<ExternalDeclaration*>
}>

NamespaceDeclaration.AnonymousNamespace = <<InlineModifier?> namespace {
	<ExternalDeclaration*>
}>

NamespaceDeclaration.NamespaceAlias = <namespace <ID> = <NamespaceID>;>

UsingDeclaration.UsingNamespace = <using namespace <NamespaceID>;>
UsingDeclaration.Using = <using <{NamespaceVarOrTypeID ","}+>;>

template options
  ID = keyword {reject}
  TypeID = keyword {reject}